# Copyright 2021 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//flutter/testing/testing.gni")

source_set("spirv") {

  sources = [
    "transpiler.h",
    "transpiler.cc",
  ]

  deps = [
    "//third_party/spirv_tools:SPIRV-Tools",
    "//third_party/spirv_headers:spv_headers",
  ]
}

if (enable_unittests) {
  executable("spirv_regenerate_goldens") {
    sources = [
      "regenerate.cc",
    ]

    deps = [
      ":spirv",
    ]
  }

  action_foreach("spirv_goldens") {
    script = "regenerate.py"

    deps = [
      ":spirv_regenerate_goldens",
    ]

    sources = [
      "goldens/glslop.spv",
      "goldens/scalar.spv",
      "goldens/simple.spv",
      "goldens/vec2op.spv",
      "goldens/vec3op.spv",
      "goldens/vec4op.spv",
    ]

    outputs = [
      "$target_gen_dir/{{source_name_part}}.sksl",
    ]

    args = [
      "./spirv_regenerate_goldens",
      "{{source}}",
      rebase_path(target_gen_dir, root_build_dir) +
        "/{{source_name_part}}.sksl",
    ]
  }

  executable("spirv_unittests") {
    testonly = true

    public_configs = [ "//flutter:export_dynamic_symbols" ]

    sources = [
      "transpiler_unittests.cc",
    ]

    deps = [
      ":spirv",
      ":spirv_goldens",
      "//third_party/googletest:gmock",
      "//third_party/googletest:gtest",
      "//third_party/googletest:gtest_main",
    ]
  }
}
